buildscript {
    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:${dependenciesManagementVersion}"
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:${spotbugsGradlePluginVersion}"
    }
}

wrapper {
    gradleVersion = "${gradleVersion}"
}

allprojects {
    repositories {
        maven {
            url 'https://maven.aliyun.com/nexus/content/groups/public'
        }
    }

    apply plugin: 'idea'
}

subprojects {
    if (project.name != 'tikoua-ui') {
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
        apply plugin: 'java'

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
        }

        dependencies {
            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.projectlombok:lombok'

            testImplementation('org.springframework.boot:spring-boot-starter-test') {
                exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
            }
        }

        test {
            useJUnitPlatform()
        }

        jar {
            enabled = true
        }

        bootJar {
            enabled = false
        }

        apply from: "${rootProject.projectDir}/gradle/checkstyle/checkstyle.gradle"
        apply from: "${rootProject.projectDir}/gradle/spotbugs/spotbugs.gradle"
        apply from: "${rootProject.projectDir}/gradle/git-hooks/git-hooks.gradle"
    }
}
